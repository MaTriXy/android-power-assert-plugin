apply from: '../metadata.gradle'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3'
    }
}

apply plugin: 'groovy'

apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "com.android.tools.build:gradle:1.1.3"

    compile 'org.javassist:javassist:[3,4)'
    compile 'org.apache.commons:commons-lang3:[3,4)'
}

// artifact settings

String git(String command) {
    println "git $command"
    def process = ["sh", "-c", "cd ${project.rootDir} ; git $command"].execute()
    if (process.waitFor() != 0) {
        throw new GradleException(process.err.text)
    }
    return process.in.text

}

task('releng') {
    description = "create a tag and push it to the remote origin"

    doLast {
        def gitStatus = git("status --porcelain")
        if (gitStatus.trim()) {
            throw new GradleException("Changes found:\n$gitStatus")
        }

        println git("tag v$VERSION")
        println git("push origin v$VERSION")
    }
}

configurations {
    mavenDeployer
}

uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = GROUP_ID
        pom.artifactId = ARTIFACT_ID
        pom.version = VERSION
    }
}

extraArchive {
    sources = true
    javadoc = true
}

modifyPom {
    project {
        name 'Android Power Assert Plugin'
        description 'Gradle plugin for Android which enables assertions and makes them much nicer.'
        url REPOSITORY_URL
        inceptionYear '2015'

        scm {
            url REPOSITORY_URL
        }

        licenses {
            license {
                name 'The MIT License'
                url 'http://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'gfx'
                name 'FUJI Goro'
                email 'g.psy.va+maven@gmail.com'
            }
        }
    }
}
